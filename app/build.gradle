

apply from: "${rootDir}/gradle/app.gradle"

dependencies {
    implementation androidxLibrary.multidex
    implementation androidxLibrary.appcompat
    implementation library.material
    implementation project(':Common')
    implementation project(':WheelView')
    implementation project(':WheelPicker')
    implementation project(':AddressPicker')
    implementation project(':ColorPicker')
    implementation project(':FilePicker')
    implementation project(':CalendarPicker')
    implementation project(':ImagePicker')
    debugRuntimeOnly library.orhanobut_logger
    debugRuntimeOnly library.xcrash
    compileOnly library.xcrash
}

android {
    defaultConfig {
        //应用的实际包名
        applicationId "com.github.yuanxia.demo"
        // 配合 productFlavors 节点按不同维度打包
        flavorDimensions "MODE"
        // 修改 AndroidManifest.xml 里的变量，占位值无法覆盖的坑可参阅 https://www.jianshu.com/p/1d5271c2c366
        manifestPlaceholders = [
                MY_CHANNEL: "unknown",
        ]
    }

    // 打包维度列表，会按维度名字母顺序执行，最终发布的具体渠道包的生成可以借助360加固等工具
    productFlavors {
        // 发布模式/正式环境
        pub {
            dimension "MODE"
            // 服务端接口地址
            buildConfigField "String", "API_BASE_URL", '"https://api.example.com"'
            // 修改 AndroidManifest.xml 里的变量
            manifestPlaceholders = [
                    MY_CHANNEL: "publisher-default",
            ]
        }
        // 开发模式/测试环境
        dev {
            dimension "MODE"
            // 服务端接口地址
            buildConfigField "String", "API_BASE_URL", '"https://test.example.com"'
            // 修改 AndroidManifest.xml 里的变量
            manifestPlaceholders = [
                    MY_CHANNEL: "developer-default",
            ]
        }
    }

}
